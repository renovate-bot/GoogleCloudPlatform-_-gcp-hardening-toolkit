/**
 * Copyright 2025 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

terraform {
  required_version = ">= 1.3.0"
  required_providers {
    google = {
      source  = "hashicorp/google"
      version = ">= 5.45.2"
    }
    random = {
      source  = "hashicorp/random"
      version = ">= 3.0"
    }
    time = {
      source  = "hashicorp/time"
      version = ">= 0.9"
    }
  }
}

resource "random_string" "constraint_suffix" {
  length  = 4
  special = false
  upper   = false
}

resource "google_org_policy_custom_constraint" "firewall_no_public_access" {
  name         = "custom.computeFirewallNoPublicAccess${random_string.constraint_suffix.result}"
  parent       = var.parent
  display_name = "Prevent VPC Firewall Rules with Public Access (0.0.0.0/0)"
  description  = "This constraint prevents the creation of VPC firewall rules that allow traffic from or to any IP address (0.0.0.0/0), enforcing the principle of least privilege."

  action_type = "DENY"

  condition = <<-EOT
    (has(resource.sourceRanges) && "0.0.0.0/0" in resource.sourceRanges) ||
    (has(resource.destinationRanges) && "0.0.0.0/0" in resource.destinationRanges)
  EOT

  method_types = [
    "CREATE",
    "UPDATE"
  ]

  resource_types = [
    "compute.googleapis.com/Firewall"
  ]
}

resource "time_sleep" "wait_for_constraint_creation" {
  create_duration = "5s"

  triggers = {
    constraint_name = google_org_policy_custom_constraint.firewall_no_public_access.name
  }
}

resource "google_org_policy_policy" "enforce_firewall_no_public_access" {
  name   = "${var.parent}/policies/${google_org_policy_custom_constraint.firewall_no_public_access.name}"
  parent = var.parent

  spec {
    rules {
      enforce = true
    }
  }
  depends_on = [time_sleep.wait_for_constraint_creation]
}
